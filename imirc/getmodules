#!/usr/bin/perl

sub BEGIN {
	my $result = "";
	my $error = "";
	my $foundconfig = 0;
	my $home = (getpwuid($>))[7];
	push @INC, "$home/.cpan";

	require CPAN;

	foreach my $dir(@INC) {
		next unless -d "$dir/CPAN";
		next unless -r "$dir/CPAN/MyConfig.pm" or -r "$dir/CPAN/Config.pm";
		my $file = -r "$dir/CPAN/MyConfig.pm" ? "MyConfig.pm" : "Config.pm";

		print "Trying to get CPAN configuration from $dir/CPAN/$file.\n";
		$INC{$file} = "$dir/CPAN/$file";
		$result = do "$dir/CPAN/$file";
		$error = $@;

		$foundconfig = 1;
		unless($error || !$result) {
			print "$dir/CPAN/$file OK\n";
			last;
		}

		$foundconfig = 0;
		delete $INC{$file};
		print STDERR "\nCouldn't get CPAN configuration from $dir/CPAN/$file:\n";
		if($error) {
			$error =~ s/^/\t/g;
			print STDERR "$error\n";
		} else {
			print STDERR "\tFilename did not return true value.\n\n";
		}
	}

	if(!$foundconfig) {
		print STDERR "We couldn't load a CPAN configuration file.\n";
		print STDERR "Run perl -MCPAN -e shell and try again.\n";
		print STDERR "You should be able to take the defaults for most options.\n";
		print STDERR "See perldoc CPAN for more information.\n";
		exit 1;
	}
}

use File::Basename;

my $altinstall = shift || 0;
$altinstall = 1 if $] < 5.006;

my $dirname = dirname($0);
chomp($dirname = `pwd`) if $dirname eq ".";
push @INC, $dirname;

my $makeargs = $CPAN::Config->{makepl_arg} = "INSTALLSITELIB=$dirname INSTALLSITEARCH=$dirname INSTALLPRIVLIB=$dirname INSTALLARCHLIB=$dirname INSTALLMAN1DIR=/tmp INSTALLMAN3DIR=/tmp" if $> != 0;
$CPAN::Config->{prerequisites_policy} = "follow";

foreach my $mod (qw(Filter::Util::Call MIME::Base64 HTML::FormatText HTML::Parse Scalar::Util Digest::MD5 Net::OSCAR HTML::Tagset)) {
	my $obj = CPAN::Shell->expand('Module', $mod);
	next if $obj->inst_version() and $mod ne "Net::OSCAR";
	print "Installing $mod...\n";
	$obj->install or $altinstall or die "Problems installing $mod - try $0 altinstall\n";
	if($altinstall) {
		my $dir = $CPAN::Config->{build_dir} . "/" . basename($obj->{CPAN_FILE});
		$dir =~ s/\.tar\.gz//;
		chdir($dir) or die "Couldn't chdir to $dir: $!\n";
		chmod(0755, ".");
		system("make distclean");
		system("perl Makefile.PL $makeargs") >> 8 and die "Problem running Makefile.PL for $mod\n";
		system("make") >> 8 and die "Problem running make for $mod\n";
		system("make install") >> 8 and die "Problem running make install for $mod\n";
	}
}

print "All modules installed!\n";
